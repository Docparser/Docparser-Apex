/* This code snippet performs a multipart/form-data HTTP request from Apex to Docparser. */
/* Creating multipart/form-dat requests is unfortunately quite complex in Apex. */
/* If you interested in more details, please visit: https://docparser.com/blog/post-file-salesforce-apex-external-http-webservices/
/* Code generously provided by Theertha Prasad */
/* Updated on Dec 2021 by MY at SSC to handle docx. Special thanks to Adheena Jacob https://developer.salesforce.com/forums/?id=9060G000000MTZ9QAO */

// Don't forget to add remote site settings
Attachment file = [SELECT ID,Name,Body FROM Attachment WHERE Id = 'FILE_ID']; // Add id filter to get proper pdf attachment
// change the following variables according to your use-case
String apiKey = 'DP_API_KEY';
String parserId = 'DP_PARSER_ID';
String targetURL = 'https://app.docparser.com/v1/document/upload/' + parserId + '?remote_id=' + file.Id;
String boundary = 'A_RANDOM_STRING';

// header
String header = '--' + boundary + '\nContent-Disposition: form-data; name="file"; filename="' + file.Name + '"\nContent-Type: multipart/form-data;'+'\nnon-svg='+True;
String headerEncoded;
do
{
    header += ' ';
    headerEncoded = EncodingUtil.base64Encode(Blob.valueOf(header + '\r\n\r\n'));
}
while(headerEncoded.endsWith('='));

// body
String footer = '--' + boundary + '--';
String bodyEncoded = EncodingUtil.base64Encode(file.Body);

if (bodyEncoded.endsWith('==')) 
{
    bodyEncoded = bodyEncoded.substring(0, bodyEncoded.length()-2) + '0K';
}
else if(bodyEncoded.endsWith('=')) 
{
    bodyEncoded = bodyEncoded.substring(0, bodyEncoded.length()-1) + 'N';
    footer = '\n' + footer;           
}
else
{
    footer = '\r\n' + footer;
}

String footerEncoded = EncodingUtil.base64Encode(Blob.valueOf(footer));
Blob bodyBlob = EncodingUtil.base64Decode(headerEncoded + bodyEncoded + footerEncoded);
System.debug('bodyBlob.size()' + bodyBlob.size());

// send
HttpRequest req = new HttpRequest();
req.setHeader('Content-Type', 'multipart/form-data; boundary=' + boundary);
req.setHeader('Authorization', 'Basic ' + apiKey);
req.setMethod('POST');
req.setEndpoint(targetURL);
req.setBodyAsBlob(bodyBlob);
req.setHeader('Content-Length', String.valueof(req.getBodyAsBlob().size()));
Http http = new Http();
HTTPResponse res = http.send(req);
System.debug('res' + res.getBody());
